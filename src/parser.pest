WHITESPACE  = _{ " " | "\t" | "\n" }

term        = @{ "\"" ~ ("\\" ~ ANY | !"\"" ~ ANY)* ~ "\"" }
nonterm     = @{ "'"  ~ ("\\" ~ ANY | !"'" ~ ANY)*  ~ "'" }
comment     = @{ "`"  ~ ("\\" ~ ANY | !"`" ~ ANY)*  ~ "`" }
empty       = @{ "!" }

sequence    =  { "[" ~ lbox_expr* ~ "]" }
stack       =  { "{" ~ comma_list ~ "}" }
choice      =  { "<" ~ comma_list ~ ">" }
comma_list  = _{ lbox_expr ~ ("," ~ lbox_expr)* }

simple_expr = _{ term | nonterm | comment | sequence | stack | choice | empty }
qmark       =  { "?" }
opt_expr    =  { simple_expr ~ qmark? }
rpt_expr    =  { opt_expr ~ ("*" ~ opt_expr)? }

lbox_expr   =  { rpt_expr ~ ("#" ~ rpt_expr)? }

root_expr   =  { lbox_expr+ }
input       = _{ root_expr ~ EOI }
